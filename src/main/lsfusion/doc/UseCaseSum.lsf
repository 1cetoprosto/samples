MODULE UseCaseSum;

//#sample1
CLASS Book 'Книга';
CLASS Category 'Категория';
  
category 'Категория' = DATA Category (Book);
//#sample1 end

//#solution1
countBooks 'Количество книг' (Category c) = GROUP SUM 1 BY category(Book book);
//#solution1 end

//#sample2
CLASS Tag 'Тег';
  
in 'Вкл' = DATA BOOLEAN (Tag, Book);
//#sample2 end

//#solution2
countBooks 'Количество книг' (Tag t) = GROUP SUM 1 IF in(t, Book b);
//#solution2 end

//#sample3
CLASS Stock 'Склад';
 
  
CLASS Ledger 'Движение';
book 'Книга' = DATA Book (Ledger);
stock 'Склад' = DATA Stock (Ledger);
  
quantity 'Кол-во' = DATA INTEGER (Ledger);
out 'Расход' = DATA BOOLEAN (Ledger);
//#sample3 end

//#solution3
TABLE bookStock (Book, Stock);
currentBalance 'Текущий остаток' (Book b, Stock s) = GROUP SUM IF out(Ledger l) THEN -quantity(l) ELSE quantity(l) BY book(l), stock(l) MATERIALIZED;
//#solution3 end

//#sample4
date 'Дата' = DATA DATE (Ledger) INDEXED; // лучше добавлять индекс, чтобы быстро шла фильтрация по дате
//#sample4 end

//#solution4
// Вариант 1
balance1 'Остаток на дату' (Book b, Stock s, DATE d) = GROUP SUM (IF out(Ledger l) THEN -quantity(l) ELSE quantity(l)) IF date(l) < d BY book(l), stock(l);
  
// Вариант 2
balance2 'Остаток на дату' (Book b, Stock s, DATE d) = currentBalance(b, s) (-) [= GROUP SUM (IF out(Ledger l) THEN -quantity(l) ELSE quantity(l)) IF date(l) >= d BY book(l), stock(l)](b, s);
//#solution4 end

//#solution5
currentBalance 'Текущий остаток' (Book b) = GROUP SUM currentBalance(b, Stock s);
//#solution5 end