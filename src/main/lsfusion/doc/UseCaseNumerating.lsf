MODULE UseCaseNumerating;

//#numbermaster
CLASS Book 'Книга';
number 'Номер' = DATA INTEGER (Book) IN recognize;
name 'Название' = DATA VARISTRING[50] (Book) IN recognize;
//#numbermaster end

//#numberaggr
book (INTEGER number) = GROUP AGGR Book b BY number(b);

bookExists (INTEGER number) = {
    IF book(number) THEN
        MESSAGE 'Книга с номером ' + number + ' существует. Ее название : ' + name(book(number)); 
    ELSE
        MESSAGE 'Книги с номером ' + number + ' не существует';
}
//#numberaggr end

//#numbergenerate
WHEN SET(Book b IS Book) AND NOT number(b) DO {
    number(b) <- (GROUP MAX number(Book bb)) (+) 1;
}
//#numbergenerate end

//#numerator
CLASS Numerator 'Нумератор';
name 'Наименование' = DATA VARISTRING[50] (Numerator) IN recognize;

value = DATA INTEGER (Numerator);
//#numerator end

//#numeratororder
CLASS Order 'Заказ';
number 'Номер' = DATA INTEGER (Order) IN recognize;

numerator 'Нумератор' = DATA Numerator (Order);  
WHEN CHANGED(numerator(Order o)) AND NOT CHANGED(number(o)) DO {
    number(o) <- value(numerator(o));
    value (Numerator n) <- value(n) (+) 1 WHERE n == numerator(o);
}
//#numeratororder end

//#numeratororderdefault
defaultNumerator 'Нумератор по умолчанию' = DATA Numerator();

WHEN SET(Order o IS Order) AND NOT CHANGED(numerator(o)) DO
    numerator(o) <- defaultNumerator();
//#numeratororderdefault end