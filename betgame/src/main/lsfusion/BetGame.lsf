MODULE WorldCup2014;

REQUIRE Authentication, Time;

CLASS Team 'Команда';
name 'Название' = DATA VARISTRING[50] (Team) IN id;

FORM team 'Команда'
    OBJECTS t = Team PANEL
    PROPERTIES(t) name
    
    EDIT Team OBJECT t
;

FORM teams 'Команды'
    OBJECTS t = Team
    PROPERTIES(t) READONLY name
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE 
    
    LIST Team OBJECT t
;

CLASS Match 'Матч';
dateTime 'Начало матча' = DATA DATETIME (Match) NONULL IN id;
started (Match m) = dateTime(m) < currentDateTime();

CONSTRAINT CHANGED(dateTime(Match m)) AND PREV(dateTime(m)) AND NOT DROPPED(m IS Match) MESSAGE 'Запрещено изменять время начала матча';

homeTeam 'Хозяева' = DATA Team (Match) NONULL;
nameHomeTeam 'Хозяева' (Match m) = name(homeTeam(m)) IN id; 

awayTeam 'Гости' = DATA Team (Match) NONULL;
nameAwayTeam 'Гости' (Match m) = name(awayTeam(m)) IN id; 

homeScore 'Результат (хозяева)' = DATA INTEGER (Match);
awayScore 'Результат (гости)' = DATA INTEGER (Match);

homeScore 'Ставка (хозяева)' = DATA INTEGER (Match, CustomUser);
awayScore 'Ставка (гости)' = DATA INTEGER (Match, CustomUser);

home 'Победа (хозяева)' (Match m) = (GROUP SUM 1 IF homeScore(m, CustomUser c) > awayScore(m, c)) IF started(m);   
tie 'Ничья' (Match m) = (GROUP SUM 1 IF homeScore(m, CustomUser c) = awayScore(m, c)) IF started(m);
away 'Победа (гости)' (Match m) = (GROUP SUM 1 IF homeScore(m, CustomUser c) < awayScore(m, c)) IF started(m);

startedHomeScore 'Ставка (хозяева)' (Match m, CustomUser c) = homeScore(m, c) IF started(m);  
startedAwayScore 'Ставка (хозяева)' (Match m, CustomUser c) = awayScore(m, c) IF started(m);  

hasBet 'Сделана ставка' (Match m, CustomUser c) = homeScore(m, c) AND awayScore(m, c);
countBets 'Кол-во ставок' (Match m) = GROUP SUM 1 IF hasBet(m, CustomUser c); 

CONSTRAINT DROPPED(Match m IS Match) AND PREV(countBets(m)) MESSAGE 'Запрещено удалять матч, по которому сделаны ставки';

CONSTRAINT CHANGED(homeScore(Match m, CustomUser c)) AND started(m) MESSAGE 'Запрещено изменять результат начавшегося матча';
CONSTRAINT CHANGED(awayScore(Match m, CustomUser c)) AND started(m) MESSAGE 'Запрещено изменять результат начавшегося матча';

CONSTRAINT CHANGED(homeScore(Match m)) AND NOT started(m) MESSAGE 'Запрещено изменять счет матча до начала матча';
CONSTRAINT CHANGED(awayScore(Match m)) AND NOT started(m) MESSAGE 'Запрещено изменять счет матча до начала матча';

currentHomeScore 'Ставка (хозяева)' (Match m) = homeScore(m, currentUser()); 
currentAwayScore 'Ставка (гости)' (Match m) = awayScore(m, currentUser());

changeCurrentHomeScore (Match m)  { 
    FOR CustomUser u == currentUser() DO
        INPUT =homeScore(m, u) CHANGE;
}

changeCurrentAwayScore (Match m)  { 
    FOR CustomUser u == currentUser() DO
        INPUT =awayScore(m, u) CHANGE;
}

// Расчет очков

cost 'Стоимость' = DATA INTEGER (Match);

correctScore (Match m, CustomUser c) = homeScore(m, c) == homeScore(m) AND awayScore(m, c) == awayScore(m); 
correctScore 'Правильный счет' (CustomUser c) = GROUP SUM 1 IF correctScore (Match m, c);
 
correctDelta (Match m, CustomUser c) = homeScore(m, c) - awayScore(m, c) == homeScore(m) -  awayScore(m); 
correctDelta 'Правильная разница' (CustomUser c) = GROUP SUM 1 IF correctDelta (Match m, c);

correctResult (Match m, CustomUser c) = (homeScore(m, c) - awayScore(m, c)) * (homeScore(m) -  awayScore(m)) > 0 OR 
                                      (homeScore(m, c) == awayScore(m, c) AND homeScore(m) == awayScore(m)); 
correctResult 'Правильный результат' (CustomUser c) = GROUP SUM 1 IF correctResult (Match m, c);

points 'Кол-во баллов' = CASE
                                            WHEN correctScore(Match m, CustomUser c) THEN 3
                                            WHEN correctDelta(m, c) THEN 2
                                            WHEN correctResult(m, c) THEN 1;

points 'Кол-во баллов' (CustomUser c) = GROUP SUM points(Match m, c); 
points 'Кол-во баллов' (Match m) = GROUP SUM points(m, CustomUser c);
pointsStarted 'Кол-во баллов' (Match m) = points(m) IF started(m); 

score 'Кол-во очков' (Match m, CustomUser c) = NUMERIC[16,4](cost(m) * points(m, c)) / points(m);
  
score 'Кол-во очков' (CustomUser c) = GROUP SUM score(Match m, c); 


FORM match 'Матч'
    OBJECTS m = Match PANEL
    PROPERTIES(m) cost, dateTime, nameHomeTeam, nameAwayTeam, homeScore, awayScore
    EDIT Match OBJECT m
;

DESIGN match {
    GROUP(,m) {
        columns = 1;
    }
}

backgroundScore = RGB(255, 224, 224);
backgroundBet = RGB(224, 224, 255);
FORM matches 'Ставки'
    OBJECTS m = Match
    PROPERTIES(m) READONLY cost, dateTime, nameHomeTeam, nameAwayTeam
    PROPERTIES(m) BACKGROUND backgroundScore() READONLY homeScore, awayScore, pointsStarted
    PROPERTIES(m) BACKGROUND backgroundBet() countBets, home, tie, away, currentHomeScore ON CHANGE changeCurrentHomeScore(m), currentAwayScore ON CHANGE changeCurrentAwayScore(m)
    PROPERTIES(m) NEWSESSION NEW, EDIT, DELETE 
    
    OBJECTS c = CustomUser GRID
    PROPERTIES(c) READONLY name[Contact]
    PROPERTIES(m, c) READONLY hasBet
    PROPERTIES(m, c) SHOWIF started(m) READONLY BACKGROUND backgroundScore() points, score 
    PROPERTIES(m, c) SHOWIF started(m) READONLY BACKGROUND backgroundBet() homeScore, awayScore
;


FORM standings 'Таблица'
    OBJECTS c = CustomUser GRID
    PROPERTIES(c) READONLY name[Contact], correctScore, correctDelta, correctResult, points, score
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE 
    
    OBJECTS m = Match GRID
    PROPERTIES(m) READONLY cost, dateTime, nameHomeTeam, nameAwayTeam
    PROPERTIES(m) BACKGROUND backgroundScore() READONLY homeScore, awayScore, pointsStarted

    PROPERTIES(m, c) READONLY BACKGROUND backgroundScore() points, score 
    PROPERTIES(m, c) READONLY BACKGROUND backgroundBet() startedHomeScore, startedAwayScore

    ORDER score(c) DESC 
;

NAVIGATOR {
    NEW matches;
    NEW standings;
    NEW teams;
} 