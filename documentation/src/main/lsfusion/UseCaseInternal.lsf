MODULE UseCaseInternal;

//#solution1
ip = DATA LOCAL TEXT();
getIPJava INTERNAL 'GetIP';
showIPJava 'Показать имя компьютера (Java)' {
    getIPJava();
    MESSAGE ip();
}

getIPFusion INTERNAL <{ findProperty("ip").change((Object)java.net.InetAddress.getLocalHost().toString(), context); }>;
showIPFusion 'Показать имя компьютера (Fusion)' {
    getIPFusion();
    MESSAGE ip();
}

FORM info 'Информация'
    PROPERTIES() showIPJava, showIPFusion
;

NAVIGATOR {
    NEW info;
}
//#solution1 end

//#solution2
gcd = DATA LOCAL INTEGER();
calculateGCD 'Рассчитать НОД' INTERNAL 'CalculateGCD' (INTEGER, INTEGER);

FORM gcd 'НОД'
    OBJECTS (a = INTEGER, b = INTEGER) PANEL
    PROPERTIES 'A' = VALUE(a), 'B' = VALUE(b)
    
    PROPERTIES gcd(), calculateGCD(a, b)
;

NAVIGATOR {
    NEW gcd;
}
//#solution2 end

//#solution3
CLASS Calculation;
a = DATA INTEGER (Calculation);
b = DATA INTEGER (Calculation);
gcd = DATA INTEGER (Calculation);
calculateGCD 'Рассчитать НОД' INTERNAL 'CalculateGCDObject' (Calculation);

EXTEND FORM gcd
    OBJECTS c = Calculation
    PROPERTIES(c) a, b, gcd, calculateGCD GRID, NEW, DELETE 
;
//#solution3 end

//#solution4
beep INTERNAL 'Beep';
FORM beep 'Сигнал'
    PROPERTIES() beep
;

NAVIGATOR {
    NEW beep;
}
//#solution4 end