MODULE UseCaseEvents;

REQUIRE Time, Authentication;

//#sample1
CLASS Order 'Заказ';
isClosed 'Закрыт' = DATA BOOLEAN (Order);
date 'Дата' = DATA DATE (Order);
number 'Номер' = DATA INTEGER (Order);
//#sample1 end

//#solution1
// Вариант 1
WHEN SET(date(Order o) < currentDate()) DO
    isClosed(o) <- TRUE;

// Вариант 2
WHEN CHANGED(currentDate()) AND date(Order o) < currentDate() DO
    isClosed(o) <- TRUE;
//#solution1 end

//#sample2
CLASS OrderDetail 'Строка заказа';
order 'Заказ' = DATA Order (OrderDetail) NONULL DELETE;

quantity 'Кол-во' = DATA NUMERIC[15,3] (OrderDetail);
price 'Цена' = DATA NUMERIC[14,2] (OrderDetail);
sum 'Сумма' = DATA NUMERIC[16,2] (OrderDetail);
//#sample2 end

//#solution2
WHEN LOCAL (CHANGED(quantity(OrderDetail d)) OR CHANGED(price(d)))
            AND NOT CHANGED(sum(d)) DO {
    sum(d) <- NUMERIC[16,2](quantity(d) * price(d));            
}
//#solution2 end

//#sample3
CLASS Book 'Книга';
name 'Наименование' = DATA ISTRING[50] (Book);
price 'Цена' = DATA NUMERIC[14,2] (Book);

book 'Книга' = DATA Book (OrderDetail);
nameBook 'Книга' (OrderDetail d) = name(book(d));

FORM order
    OBJECTS o = Order PANEL
    PROPERTIES(o) date, number
    
    OBJECTS d = OrderDetail
    PROPERTIES(d) nameBook, quantity, price, sum
    FILTERS order(d) == o
;
//#sample3 end

//#solution3
WHEN LOCAL FORMS order SETCHANGED(book(OrderDetail d)) DO {
    price(d) <- price(book(d));
}  
//#solution3 end

//#solution4
CLASS OrderLog 'Лог удаления заказа';
date 'Дата заказа' = DATA DATE (OrderLog);
number 'Номер заказа' = DATA INTEGER (OrderLog);

dateTime 'Дата' = DATA DATETIME (OrderLog);
user 'Пользователь' = DATA User (OrderLog);

WHEN DROPPED(Order o IS Order) DO {
    NEW l = OrderLog {
        date(l) <- PREV(date(o));
        number(l) <- PREV(number(o));
        
        dateTime(l) <- currentDateTime();
        user(l) <- currentUser();
    }
}
//#solution4 end