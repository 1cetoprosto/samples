MODULE UseCaseInheritance;

//#item
CLASS Item 'Товар';
name 'Имя' = DATA ISTRING[50] (Item) IN id;
FORM items 'Товары'
    OBJECTS i = Item
    PROPERTIES(i) name, NEW, DELETE
;
NAVIGATOR {
    NEW items;
}
//#item end

//#receipt
CLASS Receipt 'Поступление';
date 'Дата' = DATA DATE (Receipt) IN id;
item 'Товар' = DATA Item (Receipt);
nameItem 'Товар' (Receipt r) = name(item(r)) IN id;
posted 'Проведен' = DATA BOOLEAN (Receipt);

FORM receipts 'Поступления'
    OBJECTS r = Receipt
    PROPERTIES(r) date, nameItem, posted, NEW, DELETE
;
NAVIGATOR {
    NEW receipts;
}
//#receipt end

//#production
CLASS Production 'Производство';
date 'Дата' = DATA DATE (Production) IN id;
item 'Товар' = DATA Item (Production);
nameItem 'Товар' (Production p) = name(item(p)) IN id;
posted 'Проведен' = DATA BOOLEAN (Production);

FORM productions 'Производства'
    OBJECTS p = Production
    PROPERTIES(p) date, nameItem, posted, NEW, DELETE
;
NAVIGATOR {
    NEW productions;
}
//#production end

//#batch
CLASS ABSTRACT Batch 'Партия';
date 'Дата' = ABSTRACT DATE (Batch) IN id;
item 'Товар' = ABSTRACT Item (Batch);
nameItem 'Товар' (Batch b) = name(item(b));
type 'Тип' = ABSTRACT STRING[30] (Batch);

FORM batches 'Партии'
    OBJECTS b = Batch
    PROPERTIES(b) READONLY date, nameItem, type, objectClassName 
;
NAVIGATOR {
    NEW batches;
}
//#batch end

//#receiptbatch
CLASS ReceiptBatch 'Партия на основе поступления';
batch (Receipt receipt) = AGGR ReceiptBatch WHERE posted(receipt);
//#receiptbatch end

//#receiptbatchextend
EXTEND CLASS ReceiptBatch : Batch;
date(ReceiptBatch rb) += date(receipt(rb));
item(ReceiptBatch rb) += item(receipt(rb));
type(ReceiptBatch rb) += 'Поступление' IF rb IS ReceiptBatch;
//#receiptbatchextend end

//#productionbatch
CLASS ProductionBatch 'Партия на основе производства';
batch (Production production) = AGGR ProductionBatch WHERE posted(production);

EXTEND CLASS ProductionBatch : Batch;
date(ProductionBatch rb) += date(production(rb));
item(ProductionBatch rb) += item(production(rb));
type(ProductionBatch rb) += 'Производство' IF rb IS ProductionBatch;
//#productionbatch end

//#userbatch
CLASS UserBatch 'Партия, созданная пользователем вручную';
date 'Дата' = DATA DATE (UserBatch) IN id;
item 'Товар' = DATA Item (UserBatch);
nameItem 'Товар' (UserBatch b) = name(item(b));

FORM userBatches 'Партии (ручные)'
    OBJECTS b = UserBatch
    PROPERTIES(b) date, nameItem, NEW, DELETE
;

NAVIGATOR {
    NEW userBatches;
}

EXTEND CLASS UserBatch : Batch;
date(UserBatch ub) += date(ub);
item(UserBatch ub) += item(ub);
type(UserBatch ub) += 'Ручные' IF ub IS UserBatch;