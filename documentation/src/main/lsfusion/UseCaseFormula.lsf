MODULE UseCaseFormula;

//#sample1
CLASS Order 'Заказ';
date 'Дата' = DATA DATE (Order);
number 'Номер' = DATA STRING[30] (Order);
  
FORM orders 'Заказы на закупку'
    OBJECTS o = Order
    PROPERTIES(o) date, number, NEW, DELETE
;

NAVIGATOR {
    NEW orders;
}
//#sample1 end

//#solution1
toISO = FORMULA STRING[10] 'to_char(($1),\'YYYY-MM-DD\')';

exportToCSV 'Экспорт в CSV' () {
    LOCAL file = FILE ();
    EXPORT CSV FROM toISO(date(Order o)), number(o) TO file;
    open(file()); 
}

EXTEND FORM orders
    PROPERTIES() exportToCSV
;
//#solution1 end

//#sample2
CLASS OrderDetail;
order = DATA Order (OrderDetail) NONULL DELETE;

quantity 'Кол-во' = DATA NUMERIC[14,3] (OrderDetail);
sum 'Сумма' = DATA NUMERIC[14,2] (OrderDetail);

EXTEND FORM orders
    OBJECTS d = OrderDetail
    PROPERTIES(d) quantity, sum, NEW, DELETE
    FILTERS order(d) = o
;
//#sample2 end

//#solution2
toString = FORMULA TEXT 'to_char($1,$2)';

exportToCSV 'Экспорт в CSV' (Order o) {
    LOCAL file = FILE ();
    EXPORT CSV FROM toISO(date(o)), number(o), toString(quantity(OrderDetail d), '999 999.999'), toString(sum(d), '999 999.99') WHERE order(d) = o TO file;
    open(file()); 
}

EXTEND FORM orders
    PROPERTIES(o) exportToCSV
;
//#solution2 end

//#solution3
onlyDigits = FORMULA NULL BOOLEAN 'CASE WHEN trim($1) ~ \'^[0-9]*$\' THEN TRUE ELSE NULL END';

EXTEND FORM orders
    PROPERTIES 'Только цифры' = onlyDigits(number(o))
;
//#solution3 end