MODULE UseCaseAssign;

REQUIRE Time;

//#sample1
CLASS Order 'Заказ';
  
CLASS Customer 'Покупатель';
name = DATA ISTRING[50] (Customer);
  
date 'Дата' = DATA DATE (Order);
  
customer 'Покупатель' = DATA Customer (Order);
nameCustomer 'Покупатель' (Order o) = name(customer(o));
  
discount 'Скидка, %' = DATA NUMERIC[5,2] (Order);
  
FORM order
    OBJECTS o = Order PANEL
    PROPERTIES(o) date, nameCustomer, discount
;
//#sample1 end

//#solution1
setDateDiscount 'Применить скидку (поздняя поставка)' (Order o)  { 
    date(o) <- sum(currentDate(), 30);
    discount(o) <- 5.0;
}
  
EXTEND FORM order
    PROPERTIES(o) setDateDiscount
;
//#solution1 end

//#sample2
CLASS Book 'Книга';
name 'Наименование' = DATA ISTRING[50] (Book);
price 'Текущая цена' (Book b) = DATA NUMERIC[14,2] (Book);
  
CLASS OrderDetail 'Строка заказа';
order 'Заказ' = DATA Order (OrderDetail) NONULL DELETE;
book 'Книга' = DATA Book (OrderDetail);
nameBook 'Книга' (OrderDetail d) = name(book(d));
  
price 'Цена' = DATA NUMERIC[14,2] (OrderDetail);
  
EXTEND FORM order
    OBJECTS d = OrderDetail
    PROPERTIES(d) nameBook, price
;
//#sample2 end

//#solution2
fillPrice 'Установить текущие цены' (Order o)  { 
    price(OrderDetail d) <- price(book(d)) WHERE order(d) == o;
}
  
EXTEND FORM order
    PROPERTIES(o) fillPrice
;
//#solution2 end