MODULE UseCaseConstraint;

REQUIRE Time;

//#sample1
CLASS Book 'Книга';
name 'Наименование' = DATA ISTRING[50] (Book) IN id;
price 'Цена' = DATA NUMERIC[14,2] (Book);
//#sample1 end

//#solution1
// Вариант 1
CONSTRAINT price(Book b) > 100
    MESSAGE 'Цена книги не может быть больше 100 рублей';

// Вариант 2
CONSTRAINT SET(price(Book b) > 100)
    MESSAGE 'Цена книги не может быть больше 100 рублей';
//#solution1 end

//#sample2
CLASS Order 'Заказ';
date 'Дата' = DATA DATE (Order) IN id;
number 'Номер' = DATA INTEGER (Order) IN id;
posted 'Проведен' = DATA BOOLEAN (Order) IN id; 
//#sample2 end

//#solution2
CONSTRAINT CHANGED(date(Order o)) AND posted(o)
    MESSAGE 'Запрещено менять дату у проведенного заказа'; 
//#solution2 end

//#solution3
CONSTRAINT DROPPED(Order o IS Order) AND PREV(date(o)) < currentDate()
    MESSAGE 'Запрещено удалять старые заказы';
//#solution3 end

//#sample4
CLASS OrderDetail 'Строка заказа';
order 'Заказ' = DATA Order (OrderDetail) NONULL DELETE;
  
book 'Книга' = DATA Book (OrderDetail) NONULL;
nameBook 'Книга' (OrderDetail d) = name(book(d)) IN id;

price 'Цена' = DATA NUMERIC[14,2] (OrderDetail);
//#sample4 end

//#solution4
CONSTRAINT price(OrderDetail d) > price(book(d)) * 1.1
    MESSAGE 'Цена в заказе не может превышать цену книги на 10%';
//#solution4 end

//#sample5
CLASS Customer 'Покупатель';
name 'Имя' = DATA ISTRING[100] (Customer);

in 'Вкл' = DATA BOOLEAN (Customer, Book);

customer 'Покупатель' = DATA Customer (OrderDetail);
//#sample5 end

//#solution5
CONSTRAINT book(OrderDetail d) AND NOT in(customer(d), book(d))
    CHECKED BY book[OrderDetail]
    MESSAGE 'В строке заказа выбрана книга, которая не разрешена для покупателя';
//#solution5 end

//#solution6
// Вариант 1
CONSTRAINT (CHANGED(book(OrderDetail d)) OR CHANGED(price(d)) OR CHANGED(posted(order(d)))) AND posted(order(d))
           AND price(d) > price(book(d)) * 1.1
           MESSAGE 'Цена в заказе не может превышать цену книги на 10%';
           
// Вариант 2
constraintBook (OrderDetail d) =
    (CHANGED(book(d)) OR CHANGED(price(d)) OR CHANGED(posted(order(d)))) AND posted(order(d)) AND price(d) > price(book(d)) * 1.1;
WHEN (GROUP MAX constraintBook(OrderDetail d)) DO {
    MESSAGE 'В строке заказа выбрана книга, которая не разрешена для покупателя по строкам : \n' + 
            (GROUP CONCAT ('Дата ' + date(order(OrderDetail d)) + '; Номер ' + number(order(d))) IF constraintBook(d), ',') NOWAIT;
    CANCEL;
}
//#solution6 end