MODULE UseCaseExport;

//#sample1
REQUIRE Time;

CLASS Book 'Книга';
name 'Наименование' = DATA VARISTRING[100] (Book) IN id;

CLASS Customer 'Покупатель';
name 'Наименование' = DATA VARISTRING[50] (Customer) IN id;
address 'Адрес' = DATA VARISTRING[50] (Customer) IN base;

CLASS Order 'Заказ';
date 'Дата' = DATA DATE (Order);
number 'Номер' = DATA VARSTRING[10] (Order);

customer 'Покупатель' = DATA Customer (Order);
nameCustomer 'Покупатель' (Order o) = name(customer(o));

CLASS OrderDetail 'Строка заказа';
order 'Заказ' = DATA Order (OrderDetail) NONULL DELETE;
  
book 'Книга' = DATA Book (OrderDetail) NONULL;
nameBook 'Книга' (OrderDetail d) = name(book(d));
  
quantity 'Количество' = DATA INTEGER (OrderDetail);
price 'Цена' = DATA NUMERIC[14,2] (OrderDetail);

FORM order 'Заказ'
    OBJECTS o = Order PANEL
    PROPERTIES(o) date, number, nameCustomer
    
    OBJECTS d = OrderDetail
    PROPERTIES(d) nameBook, quantity, price, NEW, DELETE
    FILTERS order(d) == o
    
    EDIT Order OBJECT o
;

FORM orders 'Заказы'
    OBJECTS o = Order
    PROPERTIES(o) READONLY date, number, nameCustomer
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
;

NAVIGATOR {
    NEW orders;
}
//#sample1 end

//#solution1
GROUP Info;
GROUP Customer : Info;

GROUP Specification;

FORM Order
    PROPERTIES timeStamp = currentDateTime() ATTR

    OBJECTS order = Order
    PROPERTIES(order) IN Info date, number
    PROPERTIES IN Customer nameCustomer(order) EXTID 'name', =address(customer(order)) EXTID 'address'
    
    PROPERTIES IN Specification count = [GROUP SUM 1 BY order(OrderDetail d)](order) ATTR 
     
    OBJECTS Detail = OrderDetail IN Specification 
    PROPERTIES(Detail) nameBook, quantity, price
    FILTERS order(Detail) = order
;

exportToXML 'Экспорт в XML' (Order o) {
    EXPORT Order OBJECTS order = o XML;
    open(exportFile());
}

EXTEND FORM orders
    PROPERTIES(o) exportToXML TOOLBAR
;
//#solution1 end

//#solution2
exportToJSON 'Экспорт в JSON' (Order o) {
    EXPORT Order OBJECTS order = o JSON;
    open(exportFile());
}

EXTEND FORM orders
    PROPERTIES(o) exportToJSON TOOLBAR
;
//#solution2 end

//#solution3
exportToCSV (DATE date) {
    LOCAL file = FILE();
    EXPORT CSV HEADER FROM number = number(order(OrderDetail d)), 
                    customer = nameCustomer(order(d)), 
                    book = nameBook(d), 
                    quantity(d), 
                    price(d) 
           WHERE date(order(d)) = date TO file;
    WRITE CLIENT DIALOG 'orders' FROM file();
}

FORM exportParameters 'Параметры'
    OBJECTS d = DATE PANEL
    PROPERTIES(d) 'Дата' = VALUE
;
exportToCSV 'Экспорт в CSV' () {
    DIALOG exportParameters OBJECTS d INPUT DO
        exportToCSV(d);
}

EXTEND FORM orders
    PROPERTIES() exportToCSV DRAW o TOOLBAR 
;
//#solution3 end