//#header
//#module
MODULE Main;
//#module end

//#require
REQUIRE Match, Utils;
//#require end
//#header end

// Команды

isCL (Tournament t) = name(t) = 'Champions League';
isEL (Tournament t) = name(t) = 'Europa League';

isUL (Tournament t) = isCL(t) OR isEL(t);
isUL (Match m) = isUL(tournament(m));  

played (Team t, Match m) = homeTeam(m) = t OR awayTeam(m) = t;
won (Team t, Match m) = (homeTeam(m) = t AND goalsHome(m) > goalsAway(m)) OR (awayTeam(m) = t AND goalsHome(m) < goalsAway(m));
draw (Team t, Match m) = played(t, m) AND goalsHome(m) = goalsAway(m);

dataMatchCoeff = DATA NUMERIC[10,1] (Round);
matchCoeff 'Коэффициент матчей' (Round r) = OVERRIDE dataMatchCoeff(r), 1.0;

wonPoints 'Очки за победы' (Season s, Team t) = 
    GROUP SUM 2 * matchCoeff(round(Match m)) IF won(t, m) AND season(m) = s AND isUL(m);
drawPoints 'Очки за ничью' (Season s, Team t) = 
    GROUP SUM 1 * matchCoeff(round(Match m)) IF draw(t, m) AND season(m) = s AND isUL(m);
matchPoints 'Очки за матчи' (Season s, Team t) = wonPoints(s, t) (+) drawPoints(s, t) MATERIALIZED;

played 'Играл' (Season s, Tournament t, Round r, Team tm) = 
    GROUP SUM 1 IF played(tm, Match m) AND round(m) = r AND tournament(m) = t AND season(m) = s;
    
bonusPoints 'Бонус за проход' = DATA NUMERIC[10,1] (Tournament, Round);
bonusPoints 'Бонусные очки' (Season s, Team tm) = GROUP SUM bonusPoints(Tournament t, Round r) IF played(s, t, r, tm) MATERIALIZED;

points 'Очки' (Season s, Team tm) = matchPoints(s, tm) (+) bonusPoints(s, tm);

matchesUL 'Матчей в еврокубках' (Season s, Team t) = GROUP SUM 1 IF played(t, Match m) AND season(m) = s AND isUL(m);
 
teams 'Клубов' (Season s, Country c) = GROUP SUM 1 IF matchesUL(s, Team t) AND country(t) = c;
totalPoints 'Очки (всего)' (Season s, Country c) = GROUP SUM points(s, Team t) IF country(t) = c;
points 'Очки' (Season s, Country c) = trunc(NUMERIC[13,4](totalPoints(s, c)) / teams(s, c), 3);

index 'Индекс' (Season s) = PARTITION SUM 1 IF s IS Season ORDER DESC s;
rating 'Рейтинг' (Country c) = GROUP SUM points(Season s, c) IF index(s) <= 5;

matches (Tournament t, Round r) = GROUP SUM 1 IF tournament(Match m) = t AND round(m) = r; 

EXTEND FORM tournament
    OBJECTS r = Round
    PROPERTIES name(r) READONLY, matchCoeff(r), bonusPoints(o, r)
    FILTERS matches(o, r)
;
 
FORM countryCoefficientUEFA 'Коэффициент стран UEFA'
    OBJECTS s = Season
    FILTERS index(s) <= 5
    
    OBJECTS c = Country
    PROPERTIES(c) READONLY name, rating
    PROPERTIES(s, c) COLUMNS (s) points HEADER 'Очки : ' + name(s), teams HEADER 'Клубы : ' + name(s)
    
    OBJECTS t = Team
    PROPERTIES(t) READONLY nameCountry, name 
    PROPERTIES(s,t) COLUMNS (s) HEADER name(s) points BACKGROUND matchesUL(s, t)
    FILTERGROUP country
        FILTER 'По стране' country(t) = c DEFAULT
        
    OBJECTS m = Match
    PROPERTIES(m) READONLY dateTime, nameTournament, nameSeason, nameRound, 
                           nameHomeTeam, 
                           goalsHome BACKGROUND goalsHome(m) > goalsAway(m), 
                           goalsAway BACKGROUND goalsHome(m) < goalsAway(m), 
                           nameAwayTeam
    FILTERS played(t, m)
    ORDER dateTime(m) DESC
;

DESIGN countryCoefficientUEFA {
    OBJECTS {
        NEW countryDetails {
            type = SPLITH;
            fill = 0.5;
            MOVE BOX(t);
            MOVE BOX(m);
        }
    }

}

NAVIGATOR {
    NEW countryCoefficientUEFA;
}