MODULE UseCaseCIO;

//#sample1
CLASS Color 'Цвет' {
    white 'Белый',
    black 'Черный'
}
  
CLASS Book 'Книга';
  
color 'Цвет' = DATA Color (Book);
//#sample1 end

//#solution1
// Вариант 1
nameColor1 'Цвет' (Book b) = staticCaption(color(b));
  
// Вариант 2
nameColor2 'Цвет' (Book b) = IF color(b) == Color.white THEN 'Белый' ELSE 'Черный';
//#solution1 end

//#sample2
CLASS Order 'Заказ';
  
sent 'Отправлен' = DATA BOOLEAN (Order);
agreed 'Согласован' = DATA BOOLEAN (Order);
accepted 'Принят' = DATA BOOLEAN (Order);
//#sample2 end

//#solution2
// Вариант 1
nameStatus1 'Статус' (Order o) = CASE WHEN accepted(o) THEN 'Принят'
                                     WHEN agreed(o) THEN 'Согласован'
                                     WHEN sent(o) THEN 'Отправлен'
                                ELSE 'Новый';
  
// Вариант 2
CLASS Status 'Статус' {
    new 'Новый',
    sent 'Отправлен',
    agreed 'Согласован',
    accepted 'Принят'
}
  
status 'Статус' (Order o) = CASE WHEN accepted(o) THEN Status.accepted
                                 WHEN agreed(o) THEN Status.agreed
                                 WHEN sent(o) THEN Status.sent
                            ELSE Status.new;
nameStatus2 'Статус' (Order o) = staticCaption(status(o));
//#solution2 end

//#solution3
dataMarkup 'Надбавка, %' = DATA NUMERIC[6,2] (Book);
 
defaultMarkup 'Надбавка по умолчанию' = DATA NUMERIC[6,2] ();
 
markup1 'Надбавка, %' (Book b) = OVERRIDE dataMarkup(b), defaultMarkup();
 
// Эквивалентно :
markup2 'Надбавка, %' (Book b) = IF dataMarkup(b) THEN dataMarkup(b) ELSE defaultMarkup();
//#solution3 end

//#sample4
CLASS Category 'Категория';

category 'Категория' = DATA Category (Book);
//#sample4 end

//#solution4
markup 'Надбавка, %' = DATA NUMERIC[6,2] (Category);
 
markup 'Надбавка, %' (Book b) = OVERRIDE dataMarkup(b), markup(category(b));
//#solution4 end

//#sample5
number 'Номер' = DATA INTEGER (Book);
//#sample5 end

//#solution5
freeNumber1 () = (GROUP MAX number(Book b)) (+) 1;
  
// Эквивалентно :
freeNumber2() = (OVERRIDE 0, (GROUP MAX number(Book b))) + 1;
//#solution5 end