MODULE UseCaseImport;

//#sample1
REQUIRE Utils;

CLASS Book 'Книга';
name 'Наименование' = DATA VARISTRING[100] (Book) IN recognize;

id 'Код' = DATA VARSTRING[20] (Book) IN recognize;
book 'Книга' (VARSTRING[20] id) = GROUP AGGR Book b BY id(b);

CLASS Order 'Заказ';
date 'Дата' = DATA DATE (Order);
number 'Номер' = DATA VARSTRING[10] (Order);
  
CLASS OrderDetail 'Строка заказа';
order 'Заказ' = DATA Order (OrderDetail) NONULL DELETE;
  
book 'Книга' = DATA Book (OrderDetail) NONULL;
nameBook 'Книга' (OrderDetail d) = name(book(d));
  
quantity 'Количество' = DATA INTEGER (OrderDetail);
price 'Цена' = DATA NUMERIC[14,2] (OrderDetail);

FORM order 'Заказ'
    OBJECTS o = Order PANEL
    PROPERTIES(o) date, number
  
    OBJECTS d = OrderDetail
    PROPERTIES(d) nameBook, quantity, price, NEW, DELETE
    FILTERS order(d) == o
  
    EDIT Order OBJECT o
;
//#sample1 end

//#solution1
importXlsx 'Импортировать из XLS' (Order o) = {
    INPUT f = EXCELFILE DO {
    
        LOCAL bookId = VARSTRING[20] (INTEGER);
        LOCAL quantity = INTEGER (INTEGER);
        LOCAL price = NUMERIC[14,2] (INTEGER);
              
        IMPORT XLS FROM f TO bookId = A, quantity = B, price = C;
        
        FOR imported(INTEGER i) NEW d = OrderDetail DO {
            order(d) <- o;
        
            book(d) <- book(bookId(i));
            quantity(d) <- quantity(i);
            price(d) <- price(i);
        } 
    }
} 

EXTEND FORM order
    PROPERTIES(o) importXlsx
;
//#solution1 end

//#sample2
serverDirectory 'Директорий на сервере, из которого импортировать заказы' = DATA VARSTRING[100] ();
//#sample2 end

//#solution2
importOrders 'Импортировать заказы из директория' () = {

    listFiles('file://' + serverDirectory());  
    
    FOR VARISTRING[255] f = fileName(INTEGER j) AND NOT fileIsDirectory(j) DO NEWSESSION {
    
        LOCAL file = CUSTOMFILE ();
        READ 'file://' + serverDirectory() + '/' + f TO file;
        
        LOCAL date = DATE (INTEGER);
        LOCAL number = VARSTRING[10] (INTEGER);
        
        LOCAL bookId = VARSTRING[20] (INTEGER);
        LOCAL quantity = INTEGER (INTEGER);
        LOCAL price = NUMERIC[14,2] (INTEGER);

        IMPORT CSV '|' NOHEADER CHARSET 'CP1251' FROM file() TO date, number, bookId, quantity, price;

        NEW o = Order {
            date(o) <- date(0);
            number(o) <- number(0);
            
            FOR imported(INTEGER i) NEW d = OrderDetail DO {
                order(d) <- o;
                
                book(d) <- book(bookId(i));
                quantity(d) <- quantity(i);
                price(d) <- price(i);
            }
        }

        APPLY;
        move('file://' + serverDirectory() + '/' + f, 'file://' + serverDirectory() + '/' + (IF canceled() THEN 'error/' ELSE 'success/') + f);
    }	
}
//#solution2 end

//#solution3
importBooks 'Импортировать книги' () = {
    LOCAL file = CUSTOMFILE ();
    READ 'jdbc:sqlserver://localhost;databaseName=books;User=import;Password=password@SELECT id, name FROM books' TO file;

    LOCAL id = VARSTRING[20] (INTEGER);
    LOCAL name = VARISTRING[100] (INTEGER);
    IMPORT TABLE FROM file() TO id, name;
    
    //создаем новые книги
    FOR id(INTEGER i) AND NOT book(id(i)) NEW b = Book DO {
        id(b) <- id(i);
    }
    
    // меняем значения
    FOR id(Book b) == id(INTEGER i) DO {
        name(b) <- name(i);
    }
    
    // удаляем книги
    DELETE Book b WHERE b IS Book AND NOT [= GROUP SUM 1 BY id(INTEGER i)](id(b));
}
//#solution3 end

FORM orders 'Заказы'
    OBJECTS o = Order
    PROPERTIES(o) READONLY date, number
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    
    PROPERTIES() serverDirectory, importOrders
;

NAVIGATOR {
    NEW orders;
}